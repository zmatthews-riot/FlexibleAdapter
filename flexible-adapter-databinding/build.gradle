apply plugin: 'com.android.library'
apply plugin: 'eu.davidea.grabver'
apply plugin:  'maven-publish'

versioning {
    major 1
    minor 0
    patch 0
    //preRelease "b4"
    saveOn "install"
}
// For maven repository
version = versioning.name

ext {
    // Library Settings
    libraryName = "FlexibleAdapter DataBinding extension"
    libraryDescription = "DataBinding extension for FlexibleAdapter project"
    libraryLabels = ['android', 'flexibleadapter', 'adapter', 'databinding', 'binding']

    publishedGroupId = "eu.davidea"
    bintrayName = "flexible-adapter-databinding"
    artifact = bintrayName
}

android {
    namespace "eu.davidea.flexibleadapter.databinding"
    compileSdkVersion rootProject.ext.targetSdk

    defaultConfig {
        minSdkVersion rootProject.ext.minSdk
        targetSdkVersion rootProject.ext.targetSdk
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    dataBinding {
        enabled = true
    }
}

dependencies {
    compileOnly project(":flexible-adapter")
    compileOnly androidx.recyclerView
    // for NestedScrollingChild3
    implementation androidx.core
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
}

if (project.hasProperty('all') || project.hasProperty('databinding')) {
    apply from: '../maven-install.gradle'
}

afterEvaluate {
    publishing {
        publications {
            // Creates a Maven publication called "release".
            release(MavenPublication) {
                from components.findByName("release")
                groupId = 'eu.davidea'
                artifactId = 'flexible-adapter-databinding'
                version = '1.0.0'
            }
        }
    }
}