apply plugin: 'com.android.library'
apply plugin: 'eu.davidea.grabver'
apply plugin:  'maven-publish'

versioning {
    major 1
    minor 0
    patch 0
    //preRelease "b6"
    saveOn "install"
}
// For maven repository
version = versioning.name

ext {
    // Library Settings
    libraryName = "FlexibleAdapter UI extension"
    libraryDescription = "UI extension for FlexibleAdapter project"
    libraryLabels = ['android', 'flexibleadapter', 'adapter', 'ui']

    publishedGroupId = "eu.davidea"
    bintrayName = "flexible-adapter-ui"
    artifact = bintrayName
}

android {
    namespace "eu.davidea.flexibleadapter.helpers"
    compileSdkVersion rootProject.ext.targetSdk

    defaultConfig {
        minSdkVersion rootProject.ext.minSdk
        targetSdkVersion rootProject.ext.targetSdk
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    compileOnly project(':flexible-adapter')
    implementation google.material
}

if (project.hasProperty('all') || project.hasProperty('ui')) {
    apply from: '../maven-install.gradle'
}

afterEvaluate {
    publishing {
        publications {
            // Creates a Maven publication called "release".
            release(MavenPublication) {
                from components.findByName("release")
                groupId = 'eu.davidea'
                artifactId = 'flexible-adapter-ui'
                version = '1.0.0'
            }
        }
    }
}